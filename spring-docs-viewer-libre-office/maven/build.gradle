plugins {
    id 'java-library'
    id 'maven-publish'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly gradleApi()
    implementation project(":spring-docs-viewer-libre-office-common")
}

def os = System.getProperty("os.name").toLowerCase()
def isWin = os.contains("win")
def isMac = os.contains("mac")
def isLinux = os.contains("nux") || os.contains("linux")

tasks.register('nativeBuild', Exec) {
    group = project.group
    description = "Builds JNI native library for LibreOffice"

    workingDir "$projectDir/native"

    def javaHome = System.getenv("JAVA_HOME") ?: System.getProperty("java.home")
    if (javaHome == null) throw new GradleException("JAVA_HOME not set or java.home not found") as Throwable

    def includeBase = "$javaHome/include"
    def includePlatform = isMac ? "darwin" : (isWin ? "win32" : "linux")

    def loInclude
    def loLib
    def outputExt

    if (isMac) {
        loInclude = "/Applications/LibreOffice.app/Contents/sdk/include"
        loLib = "/Applications/LibreOffice.app/Contents/Frameworks"
        outputExt = "dylib"
    } else if (isLinux) {
        loInclude = "/usr/lib/libreoffice/sdk/include"
        loLib = "/usr/lib/libreoffice/program"
        outputExt = "so"
    } else if (isWin) {
        loInclude = "C:/Program Files/LibreOffice/sdk/include"
        loLib = "C:/Program Files/LibreOffice/program"
        outputExt = "dll"
    } else {
        throw new GradleException("Unsupported OS: $os")
    }

    def srcFile = "$projectDir/native/libreoffice_jni.c"
    def outputFile = "$projectDir/native/libreoffice_jni.${outputExt}"

    if (isWin) {
        commandLine 'cmd', '/c', 'gcc', '-shared',
                '-I' + includeBase,
                '-I' + includeBase + '/' + includePlatform,
                '-I' + loInclude,
                '-L' + loLib,
                '-llibreofficekitgtk',
                srcFile,
                '-o', outputFile
    } else {
        commandLine 'bash', '-c', """
            gcc -fPIC -shared \
            -I${includeBase} \
            -I${includeBase}/${includePlatform} \
            -I${loInclude} \
            -L${loLib} -llibreofficekitgtk \
            ${srcFile} -o ${outputFile}
        """
    }
}

// 빌드 후 결과물을 리소스로 복사
tasks.register('copyNativeToResources', Copy) {
    dependsOn nativeBuild
    from("$projectDir/native") {
        include('*.dll', '*.so', '*.dylib')
    }
    into("$projectDir/src/main/resources/native/" + (
            isLinux ? 'linux' :
                    isMac ? 'macos' :
                            'windows'))
}

processResources.dependsOn(copyNativeToResources)

tasks.withType(Jar).configureEach {
    manifest {
        attributes(
                'Implementation-Title': 'LibreOffice JNI Builder',
                'Implementation-Version': project.version
        )
    }
}

// Maven 배포 설정
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = 'libreoffice-jni'
            version = project.version
        }
    }

    repositories {
        mavenLocal()
    }
}